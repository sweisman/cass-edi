\                                                19:06 12-13-90
(



AREA23.SCR
CALIBRATION RUNS TO DETERMINE AREA/COUNTS IN LIMITED
REGION THEN AREA/COUNTS  *  1,2,4,8 SECTOR COUNTS=
EFFECTIVE AREA 1,2,4,8






)
\                                                16:11 12-14-90
: LOAD-CALIB-VOLTAGES
 1100E 1 V!
    0E 2 V!
    0E 3 V!
  900E 4 V!
    0E 5 V!
  734E 6 V!
 -200E 7 V!
 -500E 8 V!
  -10E 9 V!
  900E 10 V!
-1000E 11 V!
    0E 12 V!
    0E 13 V!
;
\                                                14:55 12-06-90
: SAVE-TEST-VOLTAGES
  V[] TEMP-CASE[] 16 FPSIZE * CMOVE
;
: RESTORE-TEST-VOLTAGES
  TEMP-CASE[] V[] 16 FPSIZE * CMOVE
;









\                                                16:17 12-14-90
: COMPUTE-DEFLECTOR-PAIR
  REAL-ANGLE   \ FETCH CURRENT ANGLE TO COMPUTE DEFLECTOR PAIR
 60E0 UDTEMP F! 60E0 LDTEMP F!
  FDUP 203E F>= IF  \ IF ANGLE > 201
     203E F- UDMULTIPLIER F@ F* \ COMPUTE UPPER DEFLECTOR
     DEFL-OFFSET F@ F+
     UDTEMP F!
  ELSE                  \ ANGLE< 201
     203E FSWAP F- LDMULTIPLIER F@ F* \ COMPUTE LOWER DEFL
     DEFL-OFFSET F@ F+
     LDTEMP F!
  THEN
  UDTEMP F@ 5 V!  \ SET UPPER DEFLECTOR VALUE INTO VALUE ARRAY
  LDTEMP F@ 2 V!  \ SET UPPER DEFLECTOR VALUE INTO VALUE ARRAY
;
\                                                11:42 12-07-90
: LATCH-VOLTAGES
  TRUE TRODE-CHANGE ! OUTPUT-DATA  \ LATCH CALIBRATION VOLTAGES
;












\                                                15:16 12-06-90
: SAVE-TEST-RASTER-VALUES
  NRASTERS @ NRASTERS-TEMP ! RASTER-DELAY @ RASTER-DELAY-TEMP !
  XMIN @ CXMIN-TEMP ! YMIN @ CYMIN-TEMP !
  XMAX @ CXMAX-TEMP ! YMAX @ CYMAX-TEMP !
  DX @ CDX-TEMP ! DY @ CDY-TEMP !
;
: RESTORE-TEST-RASTER-VALUES
  NRASTERS-TEMP @ NRASTERS ! RASTER-DELAY-TEMP @ RASTER-DELAY !
  CXMIN-TEMP @ XMIN ! CYMIN-TEMP @ YMIN !
  CXMAX-TEMP @ XMAX ! CYMAX-TEMP @ YMAX !
  CDX-TEMP @ DX ! CDY-TEMP @ DY !
;



\                                                12:11 01-15-91
DECIMAL
: SET-CAL-RASTER-VALUES
2468  XMIN !
2578  XMAX !
3378  YMIN !
3428  YMAX !
NCAL-RASTERS @ NRASTERS !
  XMAX @ XMIN @ - YMAX @ YMIN @ - * S>F 10000E F/
  CALIB-AREA F!
;





\                                                12:11 01-15-91
: CALCULATE-AREAS
  NCAL-RASTERS @ S>F
  CALIB-AREA F@ F* CALIB-SUM F@ F/
  NRASTERS @ S>F F/
  CALIB-VAL F!
  1ANO @ S>F CALIB-VAL F@ F* 1AREA F!
  2ANO @ S>F CALIB-VAL F@ F* 2AREA F!
  4ANO @ S>F CALIB-VAL F@ F* 4AREA F!
  8ANO @ S>F CALIB-VAL F@ F* 8AREA F!
;





\                                                12:12 01-15-91
: GET-AREAS   \ CALCULATES EFFECTIVE AREA OF A SETTING
  SAVE-TEST-VOLTAGES         \ SAVE CURRENT TEST CASE VOLTAGES
  LOAD-CALIB-VOLTAGES        \ LOAD CALIBRATION VOLTAGES
  COMPUTE-DEFLECTOR-PAIR     \ CALCULATE DEFLECTOR PAIR
  LATCH-VOLTAGES             \ LATCH CALIBRATION VOLTAGES
  SAVE-TEST-RASTER-VALUES
  SET-CAL-RASTER-VALUES      \ SET CALIBRATION RASTER LIMITS
  RASTER-ACCUM DO-HISTOGRAM  \ GET AREA CALIBRATION COUNTS
  8ANO @ S>F CALIB-SUM F!   \ STORE AREA CALIB COUNTS
  RESTORE-TEST-VOLTAGES      \ RESTORE TEST CASE
  LATCH-VOLTAGES             \ LATCH TEST VOLTAGES
  RESTORE-TEST-RASTER-VALUES \ RESTORE TEST RASTER BOX
  RASTER-ACCUM DO-HISTOGRAM  \ NOW TEST CASE
  CALCULATE-AREAS FALSE RASTER-ON !  ;

