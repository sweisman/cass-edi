\ Electron Drift Instrument                      13:44 12-26-90
(
Source Files
WELCOME TO EDI CASS TEAM

DRIVERS FOR HIGH VOLTAGE SYSTEM FOR EDI OPTICS

BY   JOE RITTER AND SLOBODAN BEROS
MODS BY SCOTT WEISMAN

LOADSAV24.SCR




 )
\                                                19:40 09-30-90
0 EQU F-TEMP














\ LOAD AND SAVE BY SRW                           16:03 10-09-90
: F-SETUP
  " .EDI" EXTEN 5 CMOVE     \ USE  .EDI FOR NEW FILES
  " *.EDI" THCB NAME>HCB    \ USE *.EDI TO SEARCH
  FHCB EQU F-TEMP             \ PLACE FHCB VAL IN TEMP
;
: CCP-SETUP
  " .CCP" EXTEN 5 CMOVE     \ USE .CCP FOR NEW FILES
  " *.CCP" THCB NAME>HCB    \ USE *.CCP TO SEARCH
  FHCB EQU F-TEMP             \ PLACE AHCB VAL IN TEMP
;
: REDRAW CLS MENU TRODE-UPDATE-ALL ;




\ LOAD AND SAVE BY SRW 3/28/90                   05:52 10-16-90

: PRINTNAMES ( --- N) \ PRINTS OUT DIR OF *.EXT
                      \ N IS NUMBER OF FILES
  0                   \ INDEX FOR NUMBER OF MATCHES
  THCB F-TEMP 0 FFIRSTF \ CHECK FOR FIRST MATCH
  BEGIN
    -1 = IF EXIT THEN \ IF NO MATCH THEN EXIT
    1+ DUP DUP 1+     \ INCREMENT INDEX DUP FOR NUM AND POS
    DUP 27 > IF 26 - 16 ELSE 0 THEN \ IF >27 FILES, NEXT COL
    0  + SWAP GOTOXY     \ GOTO POSITION ( + ADDS COL. OFFSET )
    . ." ) "          \ PRINT "N )"
    F-TEMP DUP .FNAME L \ PRINT FILENAME
    FNEXTF            \ NEXT FILE
  AGAIN
;
\ LOAD AND SAVE BY SRW                           14:35 12-26-90
: DELFILE ( --- ) \ RETURNS LENGTH OF FILENAME
  1 21 GOTOXY
  ."      ENTER FILE.EXT TO DELETE OR <CR> TO EXIT: "
  FNAME 12 GETSTRING
  1 21 GOTOXY 50 SPACES
  FNAME DUP C@ 0= IF DROP EXIT THEN \ IF LEN 0 THEN EXIT
  F-TEMP NAME>HCB               \ INSERTS FNAME INTO FHCB
  F-TEMP FDEL DROP              \ DELETE FILE & DROP STATUS
;






\ LOAD AND SAVE BY SRW 3/28/90                   13:46 12-26-90
: OPENFILE ( N MODE --- ) \ OPENS NTH FILE OF *.EXT
  SWAP THCB F-TEMP 0 FFIRSTF DROP \ THIS AND ?DO LOOP ARE TO
  1 ?DO                         \ GET THE NTH MATCH OF *.EXT
    F-TEMP FNEXTF DROP
  LOOP
  F-TEMP SWAP FOPEN               \ THEN OPEN NTH FILE, R OR W
  -1 = IF ." ERROR OPENING FILE" L THEN ;

: RANGE-CHK ( #FILES CHOICE --- CHOICE T/F ) \ CH<=#FILES
  SWAP OVER >=
  IF              \ CHK <#FILES
    TRUE
  ELSE
    DROP FALSE
  THEN ;
\ LOAD AND SAVE BY SRW 3/28/90                   17:20 12-10-90
: LOADPARAM ( N --- )
  FHCB SWAP
  1- 144 * 0 0                  \ 1ST 0 TO MAKE DBL, 2ND IS MODE
  FSEEK 2DROP                   \ POSITION
  FHCB PARAM 144 FREAD DROP     \ READ
  PARAM 16 + V[] 8 + 112 CMOVE  \ MOVE PARAMS TO VAL ARRAY
;
: GETNUM ( LIM --- N ) \ N<=LIM
  BEGIN                \ INPUT NUM AND CHECK RANGE
    7 EMIT 35 27 GOTOXY ." LOAD FROM WHICH FILE? "
    35 28 GOTOXY ." OR <CR> TO RETURN: "
    #IN L 2DUP >=      \ LIM N LIM N --- LIM N LIM>=N
    DUP IF ELSE NIP THEN
  UNTIL NIP ;          \ CONT UNTIL CORRECT RANGE

\ LOAD AND SAVE BY SRW 3/28/90                   17:55 09-12-90
: LD-OPEN ( --- F-NO FILE/T-YES FILE )
  F-SETUP                     \ SETUP EXTENSION TO USE
  PRINTNAMES DUP 0=
  IF
    15 1 GOTOXY ." NO DATA FILES!"
    7 EMIT L 30000 0 DO LOOP DROP
    FALSE
  ELSE
    GETNUM                    \ GET NUM <= # OF FILES
    DUP 0= IF DROP FALSE ELSE \ IF <CR> THEN EXIT
    L 0 OPENFILE TRUE THEN    \ ELSE OPEN FILE FOR READ
  THEN
  REDRAW ;


\ LOAD AND SAVE BY SRW 3/28/90                   16:56 09-12-90

: PARAMSCRN ( N --- M  ) \ N IS # OF CASES TO BE PRINTED
                         \ M IS # AFTER PRINT 1 SCREEN OR 0
  DUP 20 <= IF 0 ELSE DUP 20 - THEN SWAP \ CHECK HOW MANY CASES
  1+ 1 ?DO                        \ AND IF<=20, 0 MEANS NO MORE
    FHCB PARAM 144 FREAD DROP     \ READ ONE VALUE SET AND NAME
    15 I GOTOXY                   \ POS CURSOR
    I . ." ) " PARAM COUNT TYPE   \ PRINT INDEX AND NAME OF SET
  LOOP
;
\ THIS ROUTINE PRINTS A SCREEN OF 20 OR LESS CASES
\ IN A FILE
\ ONLY CALLED BY PRINTPARAMS


\ LOAD AND SAVE BY SRW 3/28/90                   15:54 10-09-90
: PRINTPARAMS ( --- N )   \ N = NUMBER OF PARAM LIST TO LOAD
  FHCB 0. 2 FSEEK DROP    \ GET FILE LENGTH LOW WORD
  FHCB 0. 0 FSEEK 2DROP   \ POSITION AT BEG OF FILE
  144 /                   \ NUM OF PARAM LISTS IN FILE
  BEGIN
    REDRAW PARAMSCRN      \ PRINT SCREEN
    15 21 GOTOXY ." ENTER NUMBER TO LOAD: "
    DUP 0<>               \ IF MORE VAL SETS LEFT LET USER KNOW
    IF 15 22 GOTOXY ." OR <CR> FOR MORE: " THEN
    #IN L DUP 0>          \ IF NOT <CR> EXIT SCREEN
    IF NIP EXIT THEN
    DROP
  AGAIN
;

\ LOAD AND SAVE BY SRW 3/28/90                   18:03 09-12-90
: LD ( --- )
  F-SETUP                     \ SETUP EXTENSION TO USE
  PRINTNAMES DUP 0=
  IF
  17 1 GOTOXY ." NO DATA FILES!" 7 EMIT L 30000 0 DO LOOP DROP
  ELSE
    GETNUM                    \ GET NUM <= # OF FILES
    DUP 0= IF DROP REDRAW EXIT THEN \ IF <CR> THEN EXIT
    L 0 OPENFILE              \ ELSE OPEN FILE FOR READ
    PRINTPARAMS               \ PRINT CASE TITLES
    LOADPARAM                 \ LOAD THE CASE
    FHCB FCLOSE DROP          \ CLOSE FILE
  THEN
  REDRAW
;
\ LOAD AND SAVE BY SRW                           14:28 12-27-90
: NEWFILE ( --- LEN ) \ RETURNS LENGTH OF FILENAME
  BEGIN
    0 21 GOTOXY ." FILE TO SAVE (NO EXT.) OR <CR> TO EXIT: "
    FNAME 8 GETSTRING
    FNAME C@ 0= IF 0 EXIT THEN   \ IF LEN 0 THEN EXIT
    FNAME EXTEN +EXT            \ ADDS STD EXT TO FNAME
    F-TEMP NAME>HCB               \ INSERTS FNAME INTO FHCB
    F-TEMP F-TEMP 0 FFIRSTF 0= IF \ CHECK IF FILE ALREADY EXISTS
      0 21 GOTOXY 50 SPACES 0 21 GOTOXY 7 EMIT
      ." FILE EXISTS! REPLACE(1=YES,0=NO)? "
    #IN IF TRUE                \ TRUE TO EXIT LOOP AND OPEN FILE
    ELSE FALSE THEN ELSE TRUE THEN \ FALSE TO REDO LOOP
  UNTIL                         \ GO THRU LOOP ONCE AGAIN
  F-TEMP 0 FMAKE DROP            \ CREATE FILE AND DROP STATUS
  FNAME C@ ;                   \ RETURN LEN OF FNAME
\ LOAD AND SAVE BY SRW                           01:59 10-04-90
: OLDFILE ( #FILES --- F-NO FILE/T-YES FILE )
  1 21 GOTOXY
  ."             SAVE TO WHICH FILE (-1 NEW, <CR> EXIT): " #IN
  DUP 0= IF 2DROP FALSE EXIT THEN \ IF <CR> DRP #&LIM & EXIT
  DUP 0< IF 2DROP NEWFILE 0=      \ GET NAME & EXIT IF NONE
         IF FALSE EXIT ELSE TRUE EXIT THEN THEN
  DUP 0> IF RANGE-CHK             \ FNUM, OPEN FOR WRITE
         IF 1 OPENFILE F-TEMP 0. 2 FSEEK 2DROP TRUE \ POS AT EOF
         ELSE FALSE THEN THEN   \ FNUM OUT OF RANGE
;





\ LOAD AND SAVE AND AN-SAV BY SRW                16:58 09-12-90
: SV-AUX ( --- ) \ CALLED BY SV,GETS TITLE,WRITES INFO TO DISK
  15 22 GOTOXY                  \ POS CURSOR
  ." CASE TITLE (MAX 15 CHARS)? "
  PARAM 15 GETSTRING            \ GET TITLE
  V[] 8 + PARAM 16 + 112 CMOVE  \ MOVE VALUE ARRAY TO PARAM
  FHCB PARAM 144 FWRITE         \ WRITE TITLE+VALS TO DISK
  FHCB FCLOSE 2DROP             \ CLOSE, DROP STAT (WRITE&CLOSE)
;







\ LOAD AND SAVE BY SRW                           17:56 09-12-90
: SV
  F-SETUP                  \ SETUP CORRECT EXTENSIONS
  PRINTNAMES DUP 0=        \ CHECK FOR FILES, SAVE #
  IF
    DROP NEWFILE          \ IF NONE DROP #, GET NEW & ADD .ANO
    0= IF REDRAW EXIT ELSE SV-AUX THEN
  ELSE
    OLDFILE             \ ASK IF SAVE TO OLD FILE
    IF SV-AUX THEN
  THEN
  REDRAW
;



