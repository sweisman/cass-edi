\ Electron Drift Instrument                      17:17 12-13-90
(
Source Files
WELCOME TO EDI CASS TEAM

DRIVERS FOR HIGH VOLTAGE SYSTEM FOR EDI OPTICS

BY   JOE RITTER, SLOBODAN BEROS, SCOTT WEISMAN

TRODES23.SCR




 )

\                                                10:16 12-28-90
DECIMAL
: TRODE-UPDATE ( --- )
  75 TRODE @ GOTOXY  \ POS AT COL ROW
  TRODE @ V@ TRODE @ 14 = IF \ IF CH#14 (ANGLE)
    74 TRODE @ GOTOXY        \ THEN DISP. IN FLOAT
    2 PLACES 6 F.R 3 PLACES
  ELSE
    75 TRODE @ GOTOXY F>S 5 .R \ ELSE DISP. AS INTEGER
  THEN
;
\ UPDATES THE ACTIVE TRODE VALUE ON THE DISPLAY




\                                                10:19 12-28-90
: TRODE-SELECT ( CH# --- )
  TRODE-UPDATE          \ UPDATE OLD SELECTION
  TRODE !               \ SAVE NEW SELECTION
  REVERSE               \ DISPLAY IN REVERSE
  TRODE-UPDATE          \ DISPLAY NEW SELECTION
  REVERSE               \ ONCE MORE TO TOGGLE TO NORMAL
;
: TRODE-UPDATE-ALL ( --- ) \ UPDATE ENTIRE DISP.(EG-AFTER LOAD)
  TRODE @ >R            \ SAV CURRENT TRODE VAL ON RETURN STACK
  15 1 DO
    I TRODE ! TRODE-UPDATE \ UPDATE ALL VALS
  LOOP
  R> DUP TRODE ! TRODE-SELECT TRUE TRODE-CHANGE !
;   \ RESTORE CURRENT TRODE VAL AND DISPLAY IN REVERSE

\                                                10:21 12-28-90
: TRODE-ADJUST-LOW ( 'Q/'q/^Q --- )
  >R TRODE @ V@ R>  \ TRODE# TRODE_VAL 'Q
  DUP 17 = IF       \ IF CH#14, USE DIFF INCREMENTS FOR ANGLE
      DROP TRODE @ 14 = IF 10.5E ELSE 100E THEN \ ^Q
  ELSE DUP 81 = IF
      DROP TRODE @ 14 = IF 3.515625E ELSE 10E THEN \ 'Q
    ELSE DUP 113 = IF
        DROP TRODE @ 14 = IF 0.3515625E ELSE 1E THEN \ 'q
      THEN
    THEN
  THEN
  F- MIN-VOLT FMAX TRODE @ V!
  REVERSE TRODE-UPDATE REVERSE
0                           \ DUMMY VALUE FOR RETURN
;
\                                                00:09 11-29-90
: TRODE-ADJUST-HI ( 'W/'w/^W --- )
  >R TRODE @ V@ R>  \ TRODE# TRODE_VAL 'W
  DUP 23 = IF
    DROP TRODE @ 14 = IF 10.5E ELSE 100E THEN
  ELSE DUP 87 = IF
      DROP TRODE @ 14 = IF 3.515625E ELSE 10E THEN
    ELSE DUP 119 = IF
        DROP TRODE @ 14 = IF 0.3515625E ELSE 1E THEN
      THEN
    THEN
  THEN
  F+ MAX-VOLT FMIN TRODE @ V!
  REVERSE TRODE-UPDATE REVERSE
0                           \ DUMMY VALUE FOR RETURN
;
\                                                17:06 12-05-90
: ENTER-VALUE ( --- )  \ NEW ROUTINE
  30 29 GOTOXY TRODE @ 14 =
  IF BEGIN
       30 29 GOTOXY
       ." ENTER ANGLE(FLOAT):"
       F#IN FDUP F0= IF FDROP FALSE ELSE TRUE THEN
    UNTIL
  ELSE ." ENTER VALUE:"
       #IN S>F MIN-VOLT FMAX MAX-VOLT FMIN
  THEN
  TRODE @ V!
  30 29 GOTOXY 36 SPACES
  REVERSE TRODE-UPDATE REVERSE
;

